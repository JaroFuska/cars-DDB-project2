

% resort(ID)
%   - info o lyziarskom stredisku

resort(spvr).  % SHOW PARADISE VELKA RACA


% segment(ID, RESORT_ID, START_POINT_ID, END_POINT_ID, DIFFICULTY, LENGTH)
%   - info o hrane, ktora predstavuje cast zjazdovky

segment(spvr_s_11, spvr, spvr_p_a, spvr_p_b, black, 70).
segment(spvr_s_10a, spvr, spvr_p_a, spvr_p_d, red, 25).
segment(spvr_s_10b, spvr, spvr_p_d, spvr_p_e, red, 10).
segment(spvr_s_10c, spvr, spvr_p_e, spvr_p_b, red, 50).
segment(spvr_s_10d, spvr, spvr_p_b, spvr_p_c, red, 100).
segment(spvr_s_n1, spvr, spvr_p_e, spvr_p_g, red, 25).
segment(spvr_s_9a, spvr, spvr_p_f, spvr_p_g, red, 50).
segment(spvr_s_9b, spvr, spvr_p_g, spvr_p_h, red, 50).
segment(spvr_s_8a, spvr, spvr_p_f, spvr_p_j, red, 40).
segment(spvr_s_8b, spvr, spvr_p_j, spvr_p_k, red, 50).
segment(spvr_s_8c, spvr, spvr_p_k, spvr_p_h, red, 30).
segment(spvr_s_n2, spvr, spvr_p_i, spvr_p_j, red, 20).
segment(spvr_s_6a, spvr, spvr_p_i, spvr_p_l, blue, 20).
segment(spvr_s_6b, spvr, spvr_p_l, spvr_p_o, blue, 50).
segment(spvr_s_7a, spvr, spvr_p_l, spvr_p_m, blue, 20).
segment(spvr_s_7b, spvr, spvr_p_l, spvr_p_m, blue, 40).
segment(spvr_s_7c, spvr, spvr_p_m, spvr_p_n, blue, 40).
segment(spvr_s_5, spvr, spvr_p_o, spvr_p_n, blue, 100).
segment(spvr_s_5_7, spvr, spvr_p_n, spvr_p_k, blue, 30).
segment(spvr_s_12, spvr, spvr_p_h, spvr_p_c, blue, 100).
segment(spvr_s_1a, spvr, spvr_p_p, spvr_p_q, blue, 50).
segment(spvr_s_1b, spvr, spvr_p_q, spvr_p_s, blue, 120).
segment(spvr_s_1c, spvr, spvr_p_q, spvr_p_r, blue, 30).
segment(spvr_s_4, spvr, spvr_p_v, spvr_p_w, blue, 20).
segment(spvr_s_2, spvr, spvr_p_p, spvr_p_s, red, 130).
segment(spvr_s_3, spvr, spvr_p_p, spvr_p_u, black, 50).

% connection(ID, RESORT_ID, START_POINT_ID, END_POINT_ID)
%   - info o hrane - lyzami prechodnom prepojeni

connection(spvr_c_1, spvr, spvr_p_a, spvr_p_f).
connection(spvr_c_2, spvr, spvr_p_f, spvr_p_i).
connection(spvr_c_3, spvr, spvr_p_p, spvr_p_o).
connection(spvr_c_4, spvr, spvr_p_t, spvr_p_p).
connection(spvr_c_5, spvr, spvr_p_n, spvr_p_s).

% skilift(ID, RESORT_ID, START_POINT_ID, END_POINT_ID, TYPE)
%  - info o hrane - lyziarskych vlekoch a lanovkach

skilift(spvr_sl_a, spvr, spvr_p_s, spvr_p_p, chairlift).
skilift(spvr_sl_b, spvr, spvr_p_u, spvr_p_p, poma).
skilift(spvr_sl_c, spvr, spvr_p_w, spvr_p_v, poma).
skilift(spvr_sl_d, spvr, spvr_p_o, spvr_p_i, poma).
skilift(spvr_sl_e, spvr, spvr_p_h, spvr_p_f, chairlift).
skilift(spvr_sl_f, spvr, spvr_p_c, spvr_p_a, chairlift).

% snow(WHERE_ID, QUALITY)
%   - kde je aka kvalita snehu

snow(spvr_s_11, good).
snow(spvr_s_10a, good).
snow(spvr_s_10b, good).
snow(spvr_s_10c, good).
snow(spvr_s_10d, good).
snow(spvr_s_n1, good).
snow(spvr_s_9a, good).
snow(spvr_s_9b, good).
snow(spvr_s_8a, good).
snow(spvr_s_8b, good).
snow(spvr_s_8c, good).
snow(spvr_s_n2, good).
snow(spvr_s_6a, good).
snow(spvr_s_6b, good).
snow(spvr_s_7a, good).
snow(spvr_s_7b, good).
snow(spvr_s_7c, good).
snow(spvr_s_5, good).
snow(spvr_s_5_7, good).
snow(spvr_s_12, good).
snow(spvr_s_1a, good).
snow(spvr_s_1b, good).
snow(spvr_s_1c, good).
snow(spvr_s_4, good).
snow(spvr_s_2, good).
snow(spvr_s_3, good).

snow(spvr_c_1, good).
snow(spvr_c_2, good).
snow(spvr_c_3, good).
snow(spvr_c_4, good).
snow(spvr_c_5, good).


% is_operating(SKILIFT_ID)
%   - info o tom, ktore vleky a lanovky su spustene

is_operating(spvr_sl_a).
is_operating(spvr_sl_b).
is_operating(spvr_sl_c).
is_operating(spvr_sl_d).
is_operating(spvr_sl_e).
is_operating(spvr_sl_f).


% pouzivatelove preferencie

user_difficulty(blue). % zjadnovky tejto obtiaznosti a lahsie
user_snow(good).          % sneh tejto kvality a lepsi

% porovnanie zlozitosti zjazdoviek

harder_than(black, red).
harder_than(red, blue).
harder_than(X, Z) :- harder_than(X, Y), harder_than(Y, Z).

% ktora zlozitost pouzivatelovi vyhovuje

acceptable_difficulty(DIF) :- user_difficulty(DIF).
acceptable_difficulty(DIF) :- acceptable_difficulty(X), harder_than(X,DIF).

% porovnanie snehu

better_than(good, poor).
better_than(poor, bad).
better_than(bad, no).
better_than(X, Z) :- better_than(X, Y), better_than(Y, Z).

% ktory sneh pouzivatelovi vyhovuje

acceptable_snow(SNOW) :- user_snow(SNOW).
acceptable_show(SNOW) :- acceptable_snow(X), better_than(SNOW, X).


% pristup k atributom

resort_id(ID) :- resort(ID).

segment_resort_id(ID, R_ID) :- segment(ID, R_ID, X, Y, DIF, LENGTH).
segment_direction(ID, X, Y) :- segment(ID, R_ID, X, Y, DIF, LENGTH).
segment_difficulty(ID, DIF) :- segment(ID, R_ID, X, Y, DIF, LENGTH).
segment_length(ID, LENGTH) :- segment(ID, R_ID, X, Y, DIF, LENGTH).

segment_node_id(ID, X) :- segment_direction(ID, X, Y).
segment_node_id(ID, Y) :- segment_direction(ID, X, Y).

connection_direction(ID, X, Y) :- connection(ID, R_ID, X, Y).

skilift_direction(ID, X, Y) :- skilift(ID, R_ID, X, Y, TYPE).


% vytvorenie grafu - berieme do uvahy preferencie pouzivatela

edge(X, Y) :-
  segment_direction(ID, X, Y),
  segment_difficulty(ID, DIF), acceptable_difficulty(DIF),
  snow(ID, SNOW), acceptable_snow(SNOW).

edge(X, Y) :-
  connection_direction(ID, X, Y),
  snow(ID, SNOW), acceptable_snow(SNOW).

edge(X, Y) :-
  skilift_direction(ID, X, Y), is_operating(ID).

% zistenie z ktoreho vrchola sa vieme do ktoreho dostat

path(X, Y) :- edge(X, Y).
path(X, Z) :- edge(X, Y), path(Y, Z).

% vytvorenie komponentov

in_component(X) :- path(X, X).
in_same_component(X, Y) :- path(X, Y), path(Y, X).

neg_component_id(X)  :- in_same_component(X, Y), X > Y.
component_id(X) :- in_component(X), not neg_component_id(X).

component_node_id(C_ID, N_ID) :- component_id(C_ID), in_same_component(C_ID, N_ID).
component_segment_id(C_ID, S_ID) :-
  segment_direction(S_ID, X, Y),
  component_node_id(C_ID, X), component_node_id(C_ID, Y).

component_resort_id(C_ID, R_ID) :-
  segment_node_id(S_ID, C_ID), segment_resort_id(S_ID, R_ID).

% celkova dlzka komponentu

component_length(C_ID, LENGTH) :-
  component_id(C_ID), LENGTH = #sum { L, S_ID : component_segment_id(C_ID, S_ID), segment_length(S_ID, L) }.

% dlzka rezortu je dlzka maximalneho komponentu

resort_length(ID, LENGTH) :-
  resort_id(ID), LENGTH = #max { L, C_ID : component_resort_id(C_ID, ID), component_length(C_ID, L) }.